
buildscript {    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
    }
}

def isNewArchitectureEnabled() {
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

apply plugin: 'com.android.library'
if (isNewArchitectureEnabled()) {
    apply plugin: 'com.facebook.react'
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    namespace "com.navigation.reactnative"
    compileSdkVersion safeExtGet("compileSdkVersion", 34)
    buildToolsVersion safeExtGet("buildToolsVersion", "28.0.3")

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 19)
        targetSdkVersion safeExtGet("targetSdkVersion", 28)
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            if (isNewArchitectureEnabled()) {
                java.excludes = [
                    '**/reactnative/NavigationPackage.java'
                ]
            } else {
               java.excludes = [
                   '**/*Module.java',
                   '**/*ViewManager.java',
                   '**/newarch/NavigationPackage.java'
                ]
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.facebook.react:react-native:+'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.fragment:fragment:1.7.0'
}

